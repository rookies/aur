# Maintainer: Robert Knauer <robert@privatdemail.net>a
# Contributor: Asa Marco <marcoasa90<at>gmail[.]com>
# Contributor: Felix Rohrbach <fxrh@gmx.de>
# Contributor: Jorge Araya AKA Shackra <jorgean@lavabit.com>

pkgname=vlmc-git
epoch=1
pkgver=0.3304.38c77f5
pkgrel=1
pkgdesc="VideoLAN Movie Creator, a simple and user-friendly video editor"
arch=('i686' 'x86_64')
url="http://vlmc.org/"
license=('GPL')
depends=('vlc' 'frei0r-plugins')
makedepends=('git' 'cmake')
source=(
  'git+git://git.videolan.org/vlmc.git'
  'http://trac.videolan.org/vlmc/raw-attachment/ticket/417/0001-Fix-build-with-gcc47.patch'
)

sha256sums=(
  'SKIP'
  '65361077dd0a7d2842640ddfccb0cd1edf4d520f55ba980d02a540a52252ee85'
)
provides=('vlmc')
conflicts=('vlmc')
_gitname="vlmc"

pkgver() {
  cd "${srcdir}/${_gitname}"
  echo "0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"
  # create work copy
  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"
  # patch!
  msg "Patching the sources (ticket #417)..."
  patch -Np1 -i "$srcdir/0001-Fix-build-with-gcc47.patch"
}

build() {
  cd "${srcdir}/${_gitname}-build"
  mkdir 'build'
  cd 'build'
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "${srcdir}/${_gitname}-build/build"
  make "DESTDIR=${pkgdir}" install
}
