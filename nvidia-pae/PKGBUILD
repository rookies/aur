# Maintainer: Robert Knauer <robert@privatdemail.net>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-pae
pkgver=331.38
_extramodules=extramodules-3.13-pae
pkgrel=2
pkgdesc="NVIDIA drivers for linux-pae"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-pae>=3.13' 'linux-pae<3.14' 'nvidia-libgl' "nvidia-utils=${pkgver}")
makedepends=('linux-pae-headers>=3.13' 'linux-pae-headers<3.14')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)
source=(
  'nvidia_3.13_kernel.patch'
)
sha256sums=(
  'fd6420117e3e243dc6c5f0b20e245d08cabd72dfd722dd3659ec34ebd6dc1d13'
)

if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '396e4d8fffacd271937ef57dd90ec2dea3ff32ee9f1de73ce5370a6e29382bf4'
  )
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '177e0d6b4cfb16889f6ba1039b5735b51b8056e5288f2b6b2943426f71744ad2'
  )
fi

prepare() {
  cd "${srcdir}"
  rm -rf "${_pkg}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}"
  patch -p1 -i "${srcdir}/nvidia_3.13_kernel.patch"
}

build() {
  _kernver="$(cat /lib/modules/${_extramodules}/version)"
  cd "${srcdir}/${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-pae.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
