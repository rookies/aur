# Maintainer: Robert Knauer <robert@privatdemail.net>
# Contributor: philomath <philomath868 AT gmail DOT com>
# Contributor: Boohbah <boohbah at gmail.com>

pkgname=objconv
pkgver=2.32
pkgrel=1
pkgdesc='Object file converter and disassembler'
arch=('i686' 'x86_64')
url="http://www.agner.org/optimize/#objconv"
license=('GPL')
depends=('gcc-libs')
makedepends=('unzip')
source=(
  "${pkgname}-${pkgver}.zip"::"http://www.agner.org/optimize/${pkgname}.zip"
)
sha256sums=(
  'e0ed4a795ce46eadbf724b072e7f1e2b4c37a8a85f94a44ee9da94ed85ac12f2'
)

prepare() {
  cd "${srcdir}"
  unzip 'source.zip' -d 'source'
}

build() {
  cd "${srcdir}/source"
  g++ -o objconv -O2 *.cpp
}

package() {
  cd "${srcdir}"
  install -D -m755 "source/objconv" "${pkgdir}/usr/bin/objconv"
  install -D -m755 "objconv-instructions.pdf" "${pkgdir}/usr/share/doc/${pkgname}/manual.pdf"
}
